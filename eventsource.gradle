import org.ajoberstar.grgit.Grgit
import org.ajoberstar.gradle.git.release.opinion.Strategies

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

plugins {
    id 'org.ajoberstar.release-opinion' version '1.3.0'
}

apply plugin: 'groovy'
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

apply from: "${rootDir}/gradle/compilestatic.gradle"

repositories {
    jcenter()
}

dependencies {
    compile "io.reactivex:rxjava:1.0.4"
    compile "org.codehaus.groovy:groovy-all:2.3.9"
    compile "org.slf4j:slf4j-api:1.7.10"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "ch.qos.logback:logback-core:1.1.3"
    testCompile "ch.qos.logback:logback-classic:1.1.3"

}

sourceCompatibility = 1.7
targetCompatibility = 1.7

release {
    // need to specify the repository to interact with
    grgit = Grgit.open(project.file('.'))

    defaultVersionStrategy = Strategies.SNAPSHOT

    // the approach to creating tags can also be modified
    tagStrategy {
        prefixNameWithV = false // defaults to true
        generateMessage = { version -> "New version: '${version.version}'" }
    }
}

apply from: "${rootDir}/gradle/bintray.gradle"
