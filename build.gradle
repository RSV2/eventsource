buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.3'
    }
}

apply plugin: 'groovy'
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

repositories {
    jcenter()
}

dependencies {
    compile "io.reactivex:rxjava:1.0.4"
    compile "org.codehaus.groovy:groovy-all:2.3.9"
    compile "org.slf4j:slf4j-api:1.7.10"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "ch.qos.logback:logback-core:1.1.3"
    testCompile "ch.qos.logback:logback-classic:1.1.3"

}

sourceCompatibility = 1.7
targetCompatibility = 1.7

// UPDATE THE VERSION!
group = 'com.thirdchannel'
version = '0.1.2'

ext.isSnapshot = version.contains('SNAPSHOT')

jar {
    manifest {
        attributes 'Implementation-Title':  'ThirdChannel Event Source',
                   'Implementation-Version': version,
                   'Specification-Version':  version
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

task createPom  {
    pom {
        project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("$buildDir/pom.xml")
}

artifactory {
    // Information around our internal artifactory. Currently we're only publishing locally as we develop this.
    contextUrl = System.getenv('ARTIFACTORY_3C_URL')
    publish {

        repository {
            repoKey = isSnapshot ? 'internal-libs-snapshot-local' : 'internal-libs-release-local'
            username = System.getenv('ARTIFACTORY_3C_USERNAME')
            password = System.getenv('ARTIFACTORY_3C_PASSWORD')
            maven = true
        }
        defaults {
            publications 'mavenJava'
            publishBuildInfo = true
            publishArtifacts = true
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}
